apply plugin: 'gaelyk'
apply plugin: 'gae-geb'
apply plugin: 'eclipse'
apply plugin: 'idea'

def compatibilityVersion = 1.6
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-gaelyk-plugin:0.4.1'
        classpath 'org.gradle.api.plugins:gradle-gae-geb-plugin:0.3'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

/*
configurations {
    precompile
}

gaelykPrecompileGroovlet.runtimeClasspath += project.configurations.precompile
gaelykPrecompileTemplate.runtimeClasspath += project.configurations.precompile
*/

dependencies {
    def gaeVersion = '1.7.4'
    def groovyVersion = '2.1.0'
    groovy "org.codehaus.groovy:groovy-all:${groovyVersion}" 
//    precompile "org.codehaus.groovy:groovy-ant:${groovyVersion}"     
//    precompile "org.ow2.asm:asm:4.0" 
//    precompile "commons-cli:commons-cli:1.2"  
    compile "org.codehaus.groovy:groovy-json:${groovyVersion}"
    compile "org.codehaus.groovy:groovy-servlet:${groovyVersion}"    
    compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVersion",
            "com.google.appengine:appengine-api-labs:$gaeVersion"
    compile 'org.gaelyk:gaelyk:2.0-SNAPSHOT'
    compile 'org.gaelyk:gaelyk-console:2.0-SNAPSHOT'

    /** 
     * To add binary plugin just declare it as a dependency. For example,
     * uncomment following to add GPars support to your Gaelyk project.
     * @see https://github.com/musketyr/gpars-appengine
     */ 
    // compile 'org.codehaus.gpars:gpars-appengine:0.1'
    
    testCompile 'org.gaelyk:gaelyk-spock:0.3.0.1'
    testCompile "com.google.appengine:appengine-api-stubs:$gaeVersion",
                "com.google.appengine:appengine-testing:$gaeVersion"
                
    functionalTestCompile 'org.codehaus.geb:geb-spock:0.7.0',
                          'org.seleniumhq.selenium:selenium-firefox-driver:2.22.0'
    gaeSdk "com.google.appengine:appengine-java-sdk:$gaeVersion"
}

idea {
    project {
        jdkName = compatibilityVersion

        ipr.withXml { provider ->
            def node = provider.asNode()

            // Set Gradle home
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
            gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
        }
    }
}

eclipse {
    project {
        name 'gaelyk-project'
        file {
            whenMerged { project ->
                project.natures << 'com.google.appengine.eclipse.core.gaeNature'
                project.natures << 'com.google.gdt.eclipse.core.webAppNature'
                project.buildCommands << [name: 'com.google.appengine.eclipse.core.enhancerbuilder']
                project.buildCommands << [name: 'com.google.appengine.eclipse.core.projectValidator']
                project.buildCommands << [name: 'com.google.gdt.eclipse.core.webAppProjectValidator']
            }
        }
    }
    classpath {
        file {
            withXml { xml ->
                xml.asNode().classpathentry.find { it.@kind == 'output' && it.@path == 'bin' }.@path = 'src/main/webapp/WEB-INF/classes'
                xml.asNode().appendNode('classpathentry', [kind: 'con', path: 'com.google.appengine.eclipse.core.GAE_CONTAINER'])
                            .appendNode('attributes')
                            .appendNode('attribute', [name: 'org.eclipse.jst.component.nondependency', value: '/src/main/webapp/WEB-INF/lib'])
                xml.asNode().appendNode('classpathentry', [exported: 'true', kind: 'con', path: 'GROOVY_SUPPORT'])
                            .appendNode('attributes')
                            .appendNode('attribute', [name: 'org.eclipse.jst.component.nondependency', value: '/src/main/webapp/WEB-INF/lib'])
            }
        }
    }
}